#  WowOpenBox by MooreaTV moorea@ymail.com (c) 2020 All rights reserved
#  Open Source Software licensed under GPLv3 - No Warranty
#  (contact the author if you need a different license)
#
#  The GNU General Public License does not permit incorporating this work
#  into proprietary programs. 
#
#  Releases detail/changes are on https://github.com/WowOpenBox/WowOpenBox/releases
#
package require Tk
package require twapi
package require TclCurl
package require Tk

# -- UI
set vers "0.11.0-pre"

proc Debug {msg} {
    global settings
    if {$settings(DEBUG)} {
        puts "DEBUG $msg"
    }
}

# Settings
# -- App utils - save settings
set app_dir [file dirname [file normalize [info script]]]

set SETTINGS_FILE [file join $app_dir "wowopenboxSettings.tcl"]

proc SaveSettings {args} {
    global SETTINGS_FILE settings
    Debug "Saving settings to $SETTINGS_FILE (from cb $args)"
    set f [open $SETTINGS_FILE w+]
    puts $f "array set settings {"
    foreach i [lsort [array names settings]] {
        #  Debug "saving $i \"$settings($i)\""
        puts $f "\t$i\t\"$settings($i)\""
    }
    puts $f "}"
    close $f
}

proc LoadSettings {} {
    global SETTINGS_FILE settings
    if {[winfo exists .b1]} {
        .b1 configure -text "Edit Settings" -command EditSettings
    }
    if {[catch {source $SETTINGS_FILE} err]} {
        puts stderr "Could not source $SETTINGS_FILE\n$err"
        return
    }
    twapi::register_hotkey $settings(hk,capture) CaptureOrUpdate
    twapi::register_hotkey $settings(hk,mouseTrack) StartStopMouseTrack
    # Set mouse control to current values
    global mouseFollow mouseRaise mouseDelay
    set mouseDelay [GetMouseDelay]
    set mouseFollow [GetFocusFollowMouse]
    set mouseRaise [GetMouseRaise]
    Debug "Settings (re)Loaded."
}


proc updateIndex {args} {
    global i maxI pos windowSize stayOnTop settings
    Debug "i is $i - args $args"
    if {[info exists settings($i,size)]} {
        set windowSize $settings($i,size)
        set pos $settings($i,posXY)
        if {[info exists settings($i,stayOnTop)]} {
            set stayOnTop $settings($i,stayOnTop)
        }
    } elseif {$i>1} {
        UpdatePos
    }
    if {$i<$maxI} {
        .l2 configure -text "Selected :"
        .b2 configure -text " Update "
    } else {
        .l2 configure -text "Next Wow :"
        .b2 configure -text " Capture "
    }
}

proc UISetup {} {
    global vers stayOnTop pos windowSize settings mouseFollow mouseRaise mouseDelay mouseCoords
    wm title . "WoW Open Box - Open Source Multiboxing"
    # Get logo
    #curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox240.png?v=$vers" -bodyvar img240
    curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox70.png?v=$vers" -bodyvar img70
    #set imgObj240 [image create photo -data $img240]
    set imgObj70 [image create photo -data $img70]
    wm iconphoto . -default $imgObj70
    grid [label .logo -image $imgObj70] -rowspan 3
    # grid [label .l1  -text "Main Menu"] -columnspan 2
    grid [button .bH -text "Help" -command Help]  -row 0 -column 1
    grid [button .b1  -text "Edit Settings" -command EditSettings]  -row 1 -column 1
    grid [button .bwl -text "Window Layout" -command WindowLayout] -row 2 -column 1
    grid [label .l2 -text "Next Wow :"] [entry .e1 -textvariable i] -sticky e
    bind .e1 <FocusIn> [list focusIn %W]
    grid [label .l3 -text "Resize to "] [entry .e2 -textvariable windowSize] -sticky e
    grid [label .l4 -text "Move to "] [entry .e3 -textvariable pos] -sticky e
    grid [checkbutton .csop -text "Stay on top" -variable stayOnTop] -columnspan 2
    grid [button .b2 -text " Capture " -command CaptureOrUpdate] -pady 5 -columnspan 2
    grid [listbox .lbw -height 6] -columnspan 2
    bind .lbw <<ListboxSelect>> [list selectChanged %W]
    grid [label .l6 -text "Mouse settings:"] -columnspan 2 -sticky w
    grid [checkbutton .mf -text "Focus follows mouse" -variable mouseFollow -command UpdateMouseFollow] -columnspan 2 -sticky w
    grid [checkbutton .mr -text "Mouse auto raises windows" -variable mouseRaise -command UpdateMouseRaise] -columnspan 2 -sticky w
    grid [label .lmd -text "Delay (ms)"] [entry .emd -textvariable mouseDelay] -sticky w
    bind .emd <Return> UpdateMouseDelay
    grid [button .bml -text "Track mouse" -command StartStopMouseTrack] [entry .emt -textvariable mouseCoords] -sticky w
    grid [label .l_bottom -text "(c) 2020 MooreaTv <moorea@ymail.com>"] -columnspan 2
}


proc mouseTrack {} {
    global mouseTrackOn mouseCoords
    set mouseCoords [twapi::get_mouse_location]
    set mouseTrackOn [after 100 mouseTrack]
}

proc StartStopMouseTrack {} {
    global mouseTrackOn
    if {$mouseTrackOn != ""} {
        Debug "Stopping mouse tracking"
        . configure -cursor arrow
        after cancel $mouseTrackOn
        set mouseTrackOn ""
        .bml configure -text "Track mouse"
    } else {
        Debug "Starting mouse tracking"
        . configure -cursor crosshair
        mouseTrack
        .bml configure -text "Stop tracking"
    }
}

proc focusIn {w args} {
    global i maxI
    Debug "got focusIn $w $args"
    if {[.lbw curselection]==""} {
        # only reset if there was a selection otherwise changing windows
        # top window clears it
        return
    }
    .lbw selection clear 0 end
    selectChanged .lbw "from focus $w"
    set i $maxI
}

proc selectChanged {w args} {
    global i maxI ww
    set sel [$w curselection]
    Debug "got selectChanged $w $args: $sel - i $i max $maxI"
    if {$sel != "" } {
        set i [expr {$sel+1}]
        # Not foreground, just focus
        FocusN $i false
    }
}

# --- utilities ---

proc GetHeight {} {
    global windowSize
    lindex $windowSize 1
}
proc GetWidth {} {
    global windowSize
    lindex $windowSize 0
}

proc GetX {} {
    global pos
    lindex $pos 0
}

proc GetY {} {
    global pos
    lindex $pos 1
}

# Does square first 4 windows
proc UpdatePos {} {
    global i pos
    set x [GetX]
    set y [GetY]
    Debug "x=$x y=$y"
    if {$i % 2} {
        incr y [GetHeight]
        Debug "incr y now $y"
    } else {
        set w [GetWidth]
        if {$i eq 4} {
            set w -$w
        }
        incr x $w
        Debug "incr x now $x"
    }
    set pos "$x $y"
}


# --- Windows API: https://twapi.magicsplat.com/v4.4/ui.html

proc FindWow {} {
    set w [twapi::find_windows -text "World of Warcraft" -visible true -single]
    return $w
}

proc Resize {w width height} {
    Debug "Resizing $w to $width x $height"
    twapi::resize_window $w $width $height
}

proc Rename {w name} {
    twapi::set_window_text $w $name
}

proc Move {w x y} {
    Debug "Moving $w to $x , $y"
    twapi::move_window $w $x $y
}

proc FindExisting {} {
    global i ww
    set n 1
    while {1} {
        set wname "Wow $n"
        set w [twapi::find_windows -match regexp -text "^$wname\$" -visible true -single]
        if {$w eq ""} {
            Debug "wow $n not found, stopping"
            return
        }
        Debug "found Wow $n!"
        updateListBox $n $w $wname
        incr n 1
    }
}

proc Update {wh x y w h top} {
    Resize $wh $w $h
    Move $wh $x $y
    if {$top} {
        twapi::set_window_zorder $wh toplayer
    } else {
        twapi::set_window_zorder $wh bottomlayer
    }
}

proc Focus {wh} {
    twapi::set_focus $wh
}

proc Foreground {wh} {
    twapi::set_foreground_window $wh
}

proc Help {} {
    global vers
    twapi::shell_execute -path https://www.wowopenbox.org/help?v=$vers
}

proc EditSettings {} {
    global SETTINGS_FILE
    Debug "Edit settings requested"
    twapi::shell_execute -path notepad.exe -params $SETTINGS_FILE
    .b1 configure -text "Reload Settings" -command LoadSettings
}

# ---

# wow windows handles
array set ww {}

proc FocusN {n fg} {
    global ww
    if {![info exists ww($n)]} {
       Debug "FocusN $n called but no such window"
    }
    Debug "FocusN $n called, found window"
    Focus $ww($n)
    if {$fg} {
        Debug "Also making $n foreground"
        Foreground $ww($n)
    }
}

proc UpdateN {n} {
    global ww settings
    if {![info exists ww($n)]} {
        Debug "Can't update non existent window $n"
        return
    }
    if {![info exists settings($n,posXY)]} {
        Debug "Can't update window $n without settings"
        return
    }
    # local/temp only
    set lpos $settings($n,posXY)
    set lsize $settings($n,size)
    set lstayOnTop 0
    if {[info exists settings($n,stayOnTop)]} {
        set lstayOnTop $settings($n,stayOnTop)
    }
    Update $ww($n) [lindex $lpos 0] [lindex $lpos 1] [lindex $lsize 0] [lindex $lsize 1] $lstayOnTop
}

proc Swap {n1 n2} {
    global ww settings
    Debug "Swap $n1<->$n2 called"
    set tmp $ww($n2)
    set ww($n2) $ww($n1)
    set ww($n1) $tmp
    UpdateN $n1
    UpdateN $n2
}

proc Capture {} {
    global i maxI settings stayOnTop
    set w [FindWow]
    if {$w eq ""} {
        tk_messageBox -type ok -icon error -title "WoW Open Box Error" -message "No World of Warcraft window found"
        return
    }
    set wname "WoW $i"
    Rename $w $wname
    Update $w [GetX] [GetY] [GetWidth] [GetHeight] $stayOnTop
    updateListBox $i $w $wname
}

proc updateListBox {n w wname} {
    global ww i maxI settings
    set ww($n) $w
    if {[info exists settings(hk$n,focus)]} {
        Debug "Setting focus hotkey for $n / $wname: $settings(hk$n,focus)"
        twapi::register_hotkey $settings(hk$n,focus) [list FocusN $n true]
    } else {
        Debug "No focus hotkey found or set for $n / $wname"
    }
    if {[info exists settings(hk$n,swap)]} {
        Debug "Setting swap hotkey for $n / $wname: $settings(hk$n,swap)"
        twapi::register_hotkey $settings(hk$n,swap) [list Swap 1 $n]
    } else {
        Debug "No focus hotkey found or set for $n / $wname"
    }
    # 0 based index
    set i0 [expr {$n-1}]
    Debug "n is $n i is $i maxI is $maxI"
    if {$n<$maxI} {
        .lbw see $i0
        return
    }
    # jump by more than 1
    for {set j $maxI} {$j < $n} {incr j} {
        .lbw insert end "WoW $j*"
    }
    .lbw insert $i0 $wname
    set i [expr {$n+1}]
    set maxI $i
}

proc CaptureOrUpdate {} {
    global i ww settings stayOnTop
    Debug "Capturing/updating $i"
    set settings($i,posXY) "[GetX] [GetY]"
    set settings($i,size) "[GetWidth] [GetHeight]"
    set settings($i,stayOnTop) $stayOnTop
    if {![info exists ww($i)]} {
        Capture
    } else {
        UpdateN $i
    }
    SaveSettings
}


proc WindowLayout {} {
    global settings
    set tw .layout
    if {[winfo exists $tw]} {
        $tw show
        return
    }
    toplevel $tw
    wm title $tw "WOB Window Layout"
    set sw [scale $tw.s -variable settings(numWindows) -orien horizontal -from 1 -to 11 -command ChangeNumWindows]
    grid [label $tw.l1 -text "How many windows:"] $sw -sticky s
    grid [canvas $tw.c -relief ridge -bd 2] -columnspan 2 -padx 5 -pady 5
    grid [button $tw.bsave -text "Save" -command SaveSettings] -pady 10 -columnspan 2
    #tk_messageBox -type ok -icon info -title "WoW Open Box Message" -message "Not implemented yet!"
    set displayInfo [twapi::get_multiple_display_monitor_info]
    Debug "displayInfo = $displayInfo"
    set n 0
    set colors {navyblue purple orange darkgreen black}
    foreach monitor $displayInfo {
        array set info $monitor
        lassign $info(-workarea) x1 y1 x2 y2
        if {$n==0} {
            set minX $x1
            set maxX $x2
            set minY $y1
            set maxY $y2
        } else {
            set minX [expr min($minX,$x1)]
            set maxX [expr max($maxX,$x2)]
            set minY [expr min($minY,$y1)]
            set maxY [expr max($maxY,$y2)]
        }
        $tw.c create rectangle $x1 $y1 $x2 $y2 -fill [lindex $colors $n]
        incr n 1
        Debug "Area $x1,$y1 - $x2,$y2"
    }
    Debug "bbox 1 [$tw.c bbox all]"
    set scale [expr {1./8}]
    $tw.c scale all 0 0 $scale $scale
    Debug "bbox 2 [$tw.c bbox all]"
    set width [expr $maxX-$minX]
    set height [expr $maxY-$minY]
    $tw.c configure -width [expr {$width*$scale+8}] -height [expr {$height*$scale+8}]
    set scrollX [expr {round($minX*-$scale)}]
    set scrollY [expr {round($minY*-$scale)}]
    set x [expr round(-1.5 * $minX * $scale)]
    set y [expr round(2.5 * ($minY+$maxY) / [winfo height $tw.c])]
    $tw.c xview scroll $x units

    bind $tw.c <ButtonPress-1> {%W scan mark   %x %y; Debug "mark %x %y"}
    bind $tw.c <B1-Motion>     {%W scan dragto %x %y 1; Debug "drag to %x %y"}
    Debug "$minX,$minY - $maxX,$maxY - $width x $height- scroll $scrollX,$scrollY   x $x y $y"
}

proc ChangeNumWindows {} {
    
}

# --- Mouse follow focus and raise control

proc GetMouseDelay {} {
    twapi::get_system_parameters_info SPI_GETACTIVEWNDTRKTIMEOUT
}

proc SetMouseDelay {v} {
    twapi::set_system_parameters_info SPI_SETACTIVEWNDTRKTIMEOUT $v
}

proc GetFocusFollowMouse {} {
    twapi::get_system_parameters_info SPI_GETACTIVEWINDOWTRACKING
}

proc SetFocusFollowMouse {v} {
    twapi::set_system_parameters_info SPI_SETACTIVEWINDOWTRACKING $v
}

proc GetMouseRaise {} {
    twapi::get_system_parameters_info SPI_GETACTIVEWNDTRKZORDER
}

proc SetMouseRaise {v} {
    twapi::set_system_parameters_info SPI_SETACTIVEWNDTRKZORDER $v
}

# -- sync with widget values

proc UpdateMouseFollow {} {
    global mouseFollow
    SetFocusFollowMouse $mouseFollow
}

proc UpdateMouseRaise {} {
    global mouseRaise
    SetMouseRaise $mouseRaise
}

proc UpdateMouseDelay {} {
    global mouseDelay
    SetMouseDelay $mouseDelay
}

#---- settings and initial setup
# globals
if {![info exists pos]} {
    # position of the next move window
    set pos "0 0"
    # size to set
    set windowSize "1920 1080"
    # stay on top
    set stayOnTop 0
    # debug on/off
    set settings(DEBUG) 0
    # numWindows
    set settings(numWindows) 5
    # start from slot 1
    # "next" window/slot #
    set i 1
    # max ever set in list box
    set maxI 1
}
# default hotkeys (change/add more in you wowopenboxSettings.tcl)
for {set n 1} {$n<=9} {incr n} {
    set settings(hk$n,focus) "Ctrl-F$n"
    set settings(hk$n,swap) "Ctrl-Shift-F$n"
}

set settings(hk,capture) "Ctrl-Shift-C"
set settings(hk,mouseTrack) "Ctrl-Shift-M"

LoadSettings

# -- get/save sequence
trace add variable i write updateIndex

# wip for layouts
#Debug "desktop workarea [twapi::get_desktop_workarea]"
#Debug "display monitors [twapi::get_display_monitors -activeonly]"
#Debug "display info [twapi::get_multiple_display_monitor_info]"

# Make it so the code can be reloaded without errors
if {![winfo exists .logo]} {
    UISetup
    set mouseTrackOn ""
    updateIndex
    # Save settings once
    SaveSettings
}

# --- main / tweak me ---
puts "WowOpenBox $vers started..."
FindExisting
