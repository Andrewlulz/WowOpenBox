#  WowOpenBox by MooreaTV moorea@ymail.com (c) 2020 All rights reserved
#  Open Source Software licensed under GPLv3 - No Warranty
#  (contact the author if you need a different license)
#
#  The GNU General Public License does not permit incorporating this work
#  into proprietary programs. 
#
#  Releases detail/changes are on https://github.com/WowOpenBox/WowOpenBox/releases
#
package require Tk
package require twapi
package require TclCurl
package require Tk

# -- UI
set vers "0.3.0"

# globals

# "next" window/slot #
set i 1
# position of the next move window
set pos "0 0"
# size to set
set settings(windowSize) "1920 1080"

# Settings
# -- App utils - save settings

set settings(DEBUG) 0

proc Debug {msg} {
    global settings
    if {$settings(DEBUG)} {
        puts "DEBUG $msg"
    }
}

set SETTINGS_FILE "~/.wowopenbox.tcl"

trace add variable settings write SaveSettings

proc SaveSettings {args} {
    global SETTINGS_FILE settings
    Debug "Saving settings to $SETTINGS_FILE (from cb $args)"
    set f [open $SETTINGS_FILE w+]
    foreach i [lsort [array names settings]] {
        Debug "set settings($i) \"$settings($i)\""
        puts $f "set settings($i) \"$settings($i)\""
    }
    close $f
}

proc LoadSettings {} {
    global SETTINGS_FILE settings
    if {[catch {source $SETTINGS_FILE} err]} {
        puts stderr "Could not source $SETTINGS_FILE\n$err"
        return
    }
}

LoadSettings

proc UISetup {} {
    global vers pos settings
    wm title . "WoW Open Box - Open Source Multiboxing"
    # Get logo
    #curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox240.png?v=$vers" -bodyvar img240
    curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox70.png?v=$vers" -bodyvar img70
    #set imgObj240 [image create photo -data $img240]
    set imgObj70 [image create photo -data $img70]
    wm iconphoto . -default $imgObj70
    grid [label .logo -image $imgObj70] -rowspan 2
    # grid [label .l1  -text "Main Menu"] -columnspan 2
    grid [button .b1  -text "Edit Window Layout" -state disabled -command WindowLayout]  -row 0 -column 1
    grid [button .bc -text Console -command {ConsoleSetup; tkcon show}] -row 1 -column 1
    grid [label .l2 -text "Next Wow is"] [entry .e1 -textvariable i] -sticky e
    grid [label .l3 -text "Resize to "] [entry .e2 -textvariable settings(windowSize)] -sticky e
    grid [label .l4 -text "Move to "] [entry .e3 -textvariable pos] -sticky e
    grid [button .b2 -text "Find & rename/resize/mode wow window" -command FindAndRename]  -columnspan 2
    grid [label .lbottom -text "(c) 2020 MooreaTv <moorea@ymail.com>"] -columnspan 2
}

# Make it so the code can be reloaded without errors
if {![winfo exists .logo]} {
    UISetup
}

# --- utilities ---

proc GetHeight {} {
    global settings
    lindex $settings(windowSize) 1
}
proc GetWidth {} {
    global settings
    lindex $settings(windowSize) 0
}

proc GetX {} {
    global pos
    lindex $pos 0
}

proc GetY {} {
    global pos
    lindex $pos 1
}

# Does square first 4 windows
proc UpdatePos {} {
    global i pos
    set x [GetX]
    set y [GetY]
    Debug "x=$x y=$y"
    if {$i % 2} {
        incr y [GetHeight]
        Debug "incr y now $y"
    } else {
        set w [GetWidth]
        if {$i eq 4} {
            set w -$w
        }
        incr x $w
        Debug "incr x now $x"
    }
    set pos "$x $y"
}


# --- Windows API: https://twapi.magicsplat.com/v4.4/ui.html

proc FindWow {} {
    set w [twapi::find_windows -text "World of Warcraft" -visible true -single]
    return $w
}

proc RenameResize {w i} {
    twapi::resize_window $w [GetWidth] [GetHeight]
    twapi::set_window_text $w "WoW $i"
}

proc Move {w x y} {
    Debug "Moving $w to $x $y"
    twapi::move_window $w $x $y
}

Debug "desktop workarea [twapi::get_desktop_workarea]"
Debug "display monitors [twapi::get_display_monitors -activeonly]"
Debug "display info [twapi::get_multiple_display_monitor_info]"

# ---

# wow windows handles
set ww {}

proc FindAndRename {} {
    global i
    set w [FindWow]
    if {$w eq ""} {
        tk_messageBox -type ok -icon error -title "WoW Open Box Error" -message "No World of Warcraft window found"
        return
    }
    RenameResize $w $i
    Move $w [GetX] [GetY]
    set ww($i) $w
    incr i 1
    UpdatePos
}

proc WindowLayout {} {
    tk_messageBox -type ok -icon info -title "WoW Open Box Message" -message "Not implemented yet!"
}

set consoleSetup false
# --- Embed tkcon console
proc ConsoleSetup {} {
    global consoleSetup appsDir
    if {$consoleSetup} {
        return
    }
    set bindir [file dirname [info nameofexecutable]]
    set appsDir [file join $bindir "../apps/"]
    Debug "Apps dir is $appsDir"
    uplevel #0 {
        source [file join $appsDir "tkcon.tcl"]
        package require tkcon
        set ::tkcon::PRIV(showOnStartup) 0
        set ::tkcon::PRIV(root) .console
        set ::tkcon::PRIV(protocol) {tkcon hide}
        #set tkcon::PRIV(name) "Wow Open Box"
        set ::tkcon::OPT(exec) ""
        tkcon::Init
        tkcon title "Wow Open Box console"
    }
    set consoleSetup true
}
# --- done with console


# --- main / tweak me ---
puts "WowOpenBox $vers started..."
