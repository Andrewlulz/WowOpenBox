#  WowOpenBox by MooreaTV moorea@ymail.com (c) 2020 All rights reserved
#  Open Source Software licensed under GPLv3 - No Warranty
#  (contact the author if you need a different license)
#
#  The GNU General Public License does not permit incorporating this work
#  into proprietary programs. 
#
#  Releases detail/changes are on https://github.com/WowOpenBox/WowOpenBox/releases
#
package require Tk
package require twapi
package require TclCurl
package require Tk

# -- UI
set vers "0.5.0"


# globals
if {![info exists pos]} {
    # position of the next move window
    set pos "0 0"
    # size to set
    set windowSize "1920 1080"
    # debug on/off
    set settings(DEBUG) 0
    # start from slot 1
    # "next" window/slot #
    set i 1
    # max ever set in list box
    set maxI 1
}

proc Debug {msg} {
    global settings
    if {$settings(DEBUG)} {
        puts "DEBUG $msg"
    }
}

# Settings
# -- App utils - save settings
set SETTINGS_FILE "~/.wowopenbox.tcl"

trace add variable settings write SaveSettings

proc SaveSettings {args} {
    global SETTINGS_FILE settings
    Debug "Saving settings to $SETTINGS_FILE (from cb $args)"
    set f [open $SETTINGS_FILE w+]
    foreach i [lsort [array names settings]] {
        Debug "set settings($i) \"$settings($i)\""
        puts $f "set settings($i) \"$settings($i)\""
    }
    close $f
}

proc LoadSettings {} {
    global SETTINGS_FILE settings
    if {[catch {source $SETTINGS_FILE} err]} {
        puts stderr "Could not source $SETTINGS_FILE\n$err"
        return
    }
}

LoadSettings

# -- get/save sequence
trace add variable i write updateIndex

proc updateIndex {args} {
    global i pos windowSize settings
    Debug "i is $i - args $args"
    if [info exists settings($i,size)] {
        set windowSize $settings($i,size)
        set pos $settings($i,posXY)
    } elseif {$i>1} {
        UpdatePos
    }
}

updateIndex

proc UISetup {} {
    global vers pos windowSize settings
    wm title . "WoW Open Box - Open Source Multiboxing"
    # Get logo
    #curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox240.png?v=$vers" -bodyvar img240
    curl::transfer -url "https://www.wowopenbox.org/WoWOpenBox70.png?v=$vers" -bodyvar img70
    #set imgObj240 [image create photo -data $img240]
    set imgObj70 [image create photo -data $img70]
    wm iconphoto . -default $imgObj70
    grid [label .logo -image $imgObj70] -rowspan 2
    # grid [label .l1  -text "Main Menu"] -columnspan 2
    grid [button .b1  -text "Edit Window Layout" -state disabled -command WindowLayout]  -row 0 -column 1
    grid [button .bc -text Console -command {ConsoleSetup; tkcon show}] -row 1 -column 1
    grid [label .l2 -text "Next Wow :"] [entry .e1 -textvariable i] -sticky e
    bind .e1 <FocusIn> [list focusIn %W]
    grid [label .l3 -text "Resize to "] [entry .e2 -textvariable windowSize] -sticky e
    grid [label .l4 -text "Move to "] [entry .e3 -textvariable pos] -sticky e
    grid [button .b2 -text "Capture" -command CaptureOrUpdate] -pady 5 -columnspan 2
    grid [listbox .lbw -height 6] -columnspan 2
    bind .lbw <<ListboxSelect>> [list selectChanged %W]
    grid [label .l_bottom -text "(c) 2020 MooreaTv <moorea@ymail.com>"] -columnspan 2
}

proc focusIn {w args} {
    Debug "got focusIn $w $args"
    if {[.lbw curselection]==""} {
        # only reset if there was a selection otherwise changing windows
        # top window clears it
        return
    }
    .lbw selection clear 0 end
    selectChanged .lbw "from focus $w"
}

proc selectChanged {w args} {
    global i maxI
    set sel [$w curselection]
    Debug "got selectChanged $w $args: $sel - i $i max $maxI"
    if {$sel == "" } {
        set i $maxI
        .l2 configure -text "Next Wow :"
        .b2 configure -text "Capture"
    } else {
        set i [expr {$sel+1}]
        .l2 configure -text "Selected :"
        .b2 configure -text "Update"
    }
}

# Make it so the code can be reloaded without errors
if {![winfo exists .logo]} {
    UISetup
}

# --- utilities ---

proc GetHeight {} {
    global windowSize
    lindex $windowSize 1
}
proc GetWidth {} {
    global windowSize
    lindex $windowSize 0
}

proc GetX {} {
    global pos
    lindex $pos 0
}

proc GetY {} {
    global pos
    lindex $pos 1
}

# Does square first 4 windows
proc UpdatePos {} {
    global i pos
    set x [GetX]
    set y [GetY]
    Debug "x=$x y=$y"
    if {$i % 2} {
        incr y [GetHeight]
        Debug "incr y now $y"
    } else {
        set w [GetWidth]
        if {$i eq 4} {
            set w -$w
        }
        incr x $w
        Debug "incr x now $x"
    }
    set pos "$x $y"
}


# --- Windows API: https://twapi.magicsplat.com/v4.4/ui.html

proc FindWow {} {
    set w [twapi::find_windows -text "World of Warcraft" -visible true -single]
    return $w
}

proc Resize {w width height} {
    twapi::resize_window $w $width $height
}

proc RenameResize {w name} {
    Resize $w [GetWidth] [GetHeight]
    twapi::set_window_text $w $name
}

proc Move {w x y} {
    Debug "Moving $w to $x $y"
    twapi::move_window $w $x $y
}

proc FindExisting {} {
    global i ww
    set n 1
    while {1} {
        set wname "Wow $n"
        set w [twapi::find_windows -match regexp -text "^$wname\$" -visible true -single]
        if {$w eq ""} {
            Debug "wow $n not found, stopping"
            return
        }
        Debug "found Wow $n!"
        updateListBox $n $w $wname
        incr n 1
    }
}

proc Update {wh x y w h} {
    Resize $wh [GetWidth] [GetHeight]
    Move $wh $x $y
}

Debug "desktop workarea [twapi::get_desktop_workarea]"
Debug "display monitors [twapi::get_display_monitors -activeonly]"
Debug "display info [twapi::get_multiple_display_monitor_info]"

# ---

# wow windows handles
array set ww {}

proc Capture {} {
    global i maxI settings
    set w [FindWow]
    if {$w eq ""} {
        tk_messageBox -type ok -icon error -title "WoW Open Box Error" -message "No World of Warcraft window found"
        return
    }
    set wname "WoW $i"
    RenameResize $w $wname
    set x [GetX]
    set y [GetY]
    Move $w $x $y
    set settings($i,posXY) "$x $y"
    set settings($i,size) "[GetWidth] [GetHeight]"
    updateListBox $i $w $wname
}

proc updateListBox {n w wname} {
    global ww i maxI
    set ww($n) $w
    # 0 based index
    set i0 [expr {$n-1}]
    Debug "n is $n i is $i maxI is $maxI"
    if {$n<$maxI} {
        .lbw see $i0
        return
    }
    # jump by more than 1
    for {set j $maxI} {$j < $n} {incr j} {
        .lbw insert end "WoW $j*"
    }
    .lbw insert $i0 $wname
    set i [expr {$n+1}]
    set maxI $i
}

proc CaptureOrUpdate {} {
    global i ww
    if {![info exists ww($i)]} {
        Capture
    } else {
        Update $ww($i) [GetX] [GetY] [GetWidth] [GetHeight]
    }
}


proc WindowLayout {} {
    tk_messageBox -type ok -icon info -title "WoW Open Box Message" -message "Not implemented yet!"
}

# --- Embed tkcon console

set consoleSetup false

proc ConsoleSetup {} {
    global consoleSetup appsDir
    if {$consoleSetup} {
        return
    }
    set bindir [file dirname [info nameofexecutable]]
    set appsDir [file join $bindir "../apps/"]
    Debug "Apps dir is $appsDir"
    uplevel #0 {
        source [file join $appsDir "tkcon.tcl"]
        package require tkcon
        set ::tkcon::PRIV(showOnStartup) 0
        set ::tkcon::PRIV(root) .console
        set ::tkcon::PRIV(protocol) {tkcon hide}
        #set tkcon::PRIV(name) "Wow Open Box"
        set ::tkcon::OPT(exec) ""
        tkcon::Init
        tkcon title "Wow Open Box console"
    }
    set consoleSetup true
}
# --- done with console


# --- main / tweak me ---
puts "WowOpenBox $vers started..."
FindExisting
